window.EDITORS = {}
window.videoDelay = 6000 # Simulate video delay

window.validateNumeric = (number) ->
  if number.toString().match(/^[-]?\d*\.?\d*$/)
    true
  else
    false

window.validateLength = (text, length, type) ->
  if type == 'greater'
    return text.length > length
  if type == 'lesser'
    return text.length < length
  if type == 'equal'
    return text.length == length

window.fileName = (path) ->
  path.replace(/.*\//, '')

window.baseName = (path) ->
  path.replace(/^.*\/|\.[^.]*$/g, '')

window.extName = (path) ->
  window.fileName(path).replace(window.baseName(path), '')

window.baseUrl = (path) ->
  path.replace(window.fileName(path), '')

window.hash_path = (url, params = {}) ->
  unless params is {}
    output = "!#{url}?"
    for key, value of params
      output += "&#{key}=#{value}"
    encodeURI(output)

try
  # Add clear method to canvas context
  CanvasRenderingContext2D::clear = (preserveTranform) ->
    if preserveTranform
      @save()
      @setTransform(1, 0, 0, 1, 0, 0)
    @clearRect(0, 0, this.canvas.width, this.canvas.height)

    if preserveTranform
      @restore()
catch e
  console?.log('No canvas support?')

# Global function to display modal dialog
window.showModal = (title, content) ->
  $('#modal').html("
    <div class='modal-header'>
      <button type='button' class='close' data-dismiss='modal'>Ã—</button>
      <h4>#{title}</h4>
    </div>
    <div class='modal-body'>
      #{content}
    </div>
    <div class='modal-footer'>
      <button class='btn' data-dismiss='modal'>Close</button>
    </div>")
  $('#modal').modal()

# Global function to display notices
window.showFlash = (msg, type) ->
  flashType = if type? then type else 'info'
  if $('#modal').is(':visible')
    $('#modal').append($('<div>').attr('class',
             "alert alert-#{flashType} flash flash-#{flashType}").
              html(msg))
    $('#modal .flash').slideDown();
    $('#modal .flash').delay(3000).slideUp();
  else
    $('#flash').html($('<div>').attr('class',
               "alert alert-#{flashType} flash flash-#{flashType}").
                html(msg))

    $('#flash .flash').slideDown().delay(3000).slideUp()

window.isBrowserSupported = ->
  window.isCanvasSupported()

window.isCanvasSupported = ->
  elem = document.createElement('canvas')
  elem.getContext?('2d')

window.isFlashSupported = ->
  FlashDetect.installed && FlashDetect.major >= 11

window.isFileReaderSupported = ->
  window.File && window.FileReader && window.FileList && window.Blob

# Initialize the global ajax loader
initLoader = ->
  $(document).ajaxStart ->
    $('body').css('cursor', 'wait')
    $('#loading-overlay').show()
    $('#loader').fadeIn()

  $(document).ajaxStop ->
    $('body').css('cursor', 'auto')
    $('#loading-overlay').hide()
    $('#loader').fadeOut(100)

# Initialize hashtag based history support
initAjaxHistory = ->
  $(window).hashchange ->
    url = location.hash.split('#!')[1]
    $.get(url, format: 'js')
  $(window).hashchange()

# Initialize scroll to top/bottom
initScroller = ->
  $('body').on('click', '.scroll-top', ->
    $('html, body').animate({scrollTop: 0}))
  $('body').on('click', '.scroll-bottom', ->
    $('html, body').animate({scrollTop: $(document).height()}))

# Initialize global tooltips
initTooltips = ->
  $(document).ajaxStart ->
    $('.tooltip').remove()
  $(document).ajaxStop ->
    $('.tooltip').remove()
    $('[rel*=tooltip]').tooltip({
      trigger: 'hover'
      placement: 'top'})

# Initialize global popovers
initPopovers = ->
  $(document).ajaxStart ->
    $('.popover').remove()
  $(document).ajaxStop ->
    $('.popover').remove()
    $('[rel*=popover]').popover({
      trigger: 'hover'
      placement: 'right'})

# Disable canvas selection (For Opera and others?)
disableCanvasSelection = ->
  $(document).ajaxStop ->
    $('canvas').bind('mousedown selectstart', (e) ->
      false)

# Initialize modal behaviors
initModals= ->
  $('body').on('shown', $('#modal'), ->
    $('#modal').find('input[type="text"]:not([readonly]):first').focus())
  $('body').on('hidden', $('#modal'), ->
    $('#modal').html(''))
  # Close modals on hash change
  $(window).hashchange( ->
    $('#modal').modal('hide'))

# Initialize richtext editor
initRichtext = ->
  $(document).ajaxStop ->
    $('textarea.rich').each ->
      $(@).richtext()

# Initialize uploaders
initRichupload = ->
  $(document).ajaxStop ->
    $('input[type=file].rich').richupload()

initMathAjax = ->
  $(document).ajaxStop ->
    MathJax.Hub.Queue(["Typeset", MathJax.Hub])

checkFlash = ->
  unless window.isFlashSupported()
    window.showModal('Flash player outdated', '
        <div class="alert center">
          Your flash player is out of date, please update it.
          If you proceed without updating, your browsing experience might be degraded.
        </div>
        <div class="center">
          <a href="https://www.adobe.com/go/getflashplayer" target="_blank">
            <img src="/assets/get_flash_player.jpg" />
          </a>
        </div>')

checkBrowser = ->
  unless window.isBrowserSupported()
    window.showModal('Web browser outdated', '
        <div class="alert center">
          Your web browser is out of date, please update it.
          If you proceed without updating, your browsing experience might be degraded.
        </div>
        <div class="center">
          <a href="http://www.google.com/chrome" target="_blank"
             style="text-decoration: none;">
            <img src="/assets/browser_chrome.png" class="img-polaroid" />
          </a>
          <a href="http://www.mozilla.com/firefox/" target="_blank"
             style="text-decoration: none;">
            <img src="/assets/browser_firefox.png" class="img-polaroid" />
          </a>
          <a href="http://www.apple.com/safari/download/" target="_blank"
             style="text-decoration: none;">
            <img src="/assets/browser_safari.png" class="img-polaroid" />
          </a>
          <a href="http://www.opera.com/download/" target="_blank"
             style="text-decoration: none;">
            <img src="/assets/browser_opera.png" class="img-polaroid" />
          </a>
          <a href="http://www.microsoft.com/windows/Internet-explorer/" target="_blank"
             style="text-decoration: none;">
            <img src="/assets/browser_msie.png" class="img-polaroid" />
          </a>
        </div>')

initChecks = ->
  checkFlash()
  checkBrowser()

initTimeago = ->
  $(document).ajaxStop ->
    $('time.timeago').timeago()
    $('abbr.timeago').timeago()

initSelectBox = ->
  $(document).ajaxStop ->
    $('select.rich').selectpicker()

initDatePicker = ->
  $(document).ajaxStop ->
    $('.dp').datepicker
      format: 'dd/mm/yyyy'

initColorBox = ->
  $(document).ajaxStop ->
    $(".group1").colorbox({rel:'group1'});

initAjaxAnalytics = ->
  $(document).ajaxStop ->
    window._gaq.push(['_trackPageview', '/' + location.hash])

window.initAjaxCSRF = (token) ->
  $.ajaxSetup
    headers:
      'X-CSRF-Token': token

disableRightClickonAjaxLink = ->
  $(document).ajaxStop ->
    $('a[data-remote=true]').on('contextmenu', (evnt) ->
      evnt.preventDefault())

disableChromeNewTabShortcut = ->
  $(document).ajaxStop ->
    $('a[data-remote=true]').on('click', (evnt) ->
      # XXX: Hack. Disable chrome's open in tab shortcut
      if evnt.ctrlKey || evnt.metaKey
        evnt.preventDefault())

# Setup everything...
$ ->
  initLoader()
  initAjaxHistory()
  initScroller()
  initTooltips()
  initPopovers()
  disableCanvasSelection()
  initModals()
  initRichtext()
  initRichupload()
  initChecks()
  initDatePicker()
  initMathAjax()
  initTimeago()
  initColorBox()
  disableRightClickonAjaxLink()
  disableChromeNewTabShortcut()
  <% if Rails.env.production? %>
  initAjaxAnalytics()
  <% end %>
  <% unless Rails.env.test? %>
  initSelectBox()
  <% end %>
